!~~~~~~~~~~~定义1、2、3、5号钢构件截面~~~~~~~~~~~!
!-------------------START-----------------------!
FINISH
/CLEAR
/INPUT,Sections,mac
!--------------------END-------------------------!

!------------------------------------------------!
FINISH
/CLEAR
/FILNAME,BeiPanJiang_Archbridge
/TITLE,The Analysis of Stiff Skeleton
/PREP7
!~~~~~~~~~~~~~定义MAT,ETYPE及参数~~~~~~~~~~~~~~~~!
ET,1,BEAM189
ET,2,SHELL63
!~~~~~~~~~~~~~钢材
MP,EX,1,2.1E11
MP,PRXY,1,0.3
MP,DENS,1,7850
!~~~~~~~~~~~~~C40（墩）、C55（梁）钢筋混凝土
MP,EX,2,3.4E10
MP,PRXY,2,0.25
MP,DENS,2,2550
!~~~~~~~~~~~~~C60钢筋混凝土
MP,EX,3,3.6E10
MP,PRXY,3,0.25
MP,DENS,3,2550
R,1,0.5
R,2,0.55
R,3,0.6
R,4,0.65
R,5,0.7
R,6,0.75
R,7,0.8
R,8,0.9
R,9,1.1
!~~~~~~~~~~~~~C80钢筋混凝土
MP,EX,4,3.8E10
MP,PRXY,4,0.25
MP,DENS,4,2550
!~~~~~~~~~~~~~杆件分段数
N=3
MA=6
!------------------------------------------------!



!~~~~~~~~~~~~~导入1-5号钢构件截面~~~~~~~~~~~~~~~~!
!-------------------START-----------------------!
!~~~~~~~~~~~~~~~~~~导入1号截面L90*90*12
SECTYPE,1,BEAM,MESH
SECOFFSET,USER
SECREAD,L90X90X12,SECT,,MESH
!SECCONTROLS,,,,63.76
!~~~~~~~~~~~~~~~~~~导入2号截面L90*90*10
SECTYPE,2,BEAM,MESH
SECOFFSET,USER
SECREAD,L90X90X10,SECT,,MESH
!SECCONTROLS,,,,53.904
!~~~~~~~~~~~~~~~~~~导入3号截面L110*110*14
SECTYPE,3,BEAM,MESH
SECOFFSET,USER
SECREAD,L110X110X14,SECT,,MESH
!SECCONTROLS,,,,91.236
!~~~~~~~~~~~~~~~~~~导入4号截面空心钢管(偏心)
!SECTYPE,4,BEAM,MESH
!SECOFFSET,USER,,-0.1
!SECREAD,GGHNT,SECT,,MESH
!SECCONTROLS,,,,429.7+987
!~~~~~~~~~~~~~~~~~~被注释掉的这一段是钢管混凝土联合截面的建立
!~~~~~~~~~~~~~~~~~~现在采用分开建立的方式，因此不用上面的这段
SECTYPE,4,BEAM,CTUBE
SECDATA,0.351,0.375
SECOFFSET,USER,,-0.1
!~~~~~~~~~~~~~~~~~~导入44号截面钢管内混凝土截面(偏心)
SECTYPE,44,BEAM,CSOLID
SECDATA,0.351
SECOFFSET,USER,,-0.1
!~~~~~~~~~~~~~~~~~~导入5号截面L90*90*12
SECTYPE,5,BEAM,MESH
SECOFFSET,USER
SECREAD,N4,SECT,,MESH
!SECCONTROLS,,,,31.88
!--------------------END-----------------------!

!----------------------------------------------------!
!~~~~~~~~~~~~~~~~~~定义交界墩截面~~~~~~~~~~~~~~~~~~~~!
!----------------------START-------------------------!
!~~~~~~~~~~~~~~~~~~定义6号截面（交界墩1-1截面）
SECTYPE,6,BEAM,RECT
SECDATA,7.5,8.37
!~~~~~~~~~~~~~~~~~~定义7号截面（交界墩2-2截面）
SECTYPE,7,BEAM,RECT
SECDATA,7.5,7.72
!~~~~~~~~~~~~~~~~~~定义8号截面（交界墩2'-2'截面）
SECTYPE,8,BEAM,HREC
SECDATA,7.5,7.72,1.5,1.5,0.9,0.9
!~~~~~~~~~~~~~~~~~~定义9号截面（交界墩3-3截面）
SECTYPE,9,BEAM,HREC
SECDATA,7.5,7.01,1.5,1.5,0.9,0.9
!~~~~~~~~~~~~~~~~~~定义10号截面（交界墩4-4截面）
SECTYPE,10,BEAM,HREC
SECDATA,7.5,5.66,1.5,1.5,0.9,0.9
!~~~~~~~~~~~~~~~~~~定义11号截面（交界墩4'-4'截面）
SECTYPE,11,BEAM,HREC
SECDATA,7.5,5.6,1.5,1.5,0.7,0.7
!~~~~~~~~~~~~~~~~~~定义12号截面（交界墩5-5截面）
SECTYPE,12,BEAM,HREC
SECDATA,7.5,5.01,1.5,1.5,0.7,0.7
!~~~~~~~~~~~~~~~~~~定义13号截面（交界墩6-6截面）
SECTYPE,13,BEAM,HREC
SECDATA,7.5,4.50,1.5,1.5,0.7,0.7
!~~~~~~~~~~~~~~~~~~定义14号截面（交界墩7-7截面）(7-7截面偏心为上部)
SECTYPE,14,BEAM,RECT
SECDATA,4.50,5.0
SECOFFSET,USER,,2.5
!~~~~~~~~~~~~~~~~~~定义15号截面（交界墩8-8截面）
SECTYPE,15,BEAM,HREC
SECDATA,4.50,4.0,0.8,0.8,0.8,0.8
!~~~~~~~~~~~~~~~~~~定义16号变截面（交界墩1-2截面）
SECTYPE,16,TAPER
SECDATA,6,-11.4,8.98,4.6
SECDATA,7,-11.4,8.51,16.6
!~~~~~~~~~~~~~~~~~~定义17号变截面（交界墩2'-3截面）
SECTYPE,17,TAPER
SECDATA,8,-11.4,8.51,16.6
SECDATA,9,-11.4,7.99,29.6
!~~~~~~~~~~~~~~~~~~定义18号变截面（交界墩3-4截面）
SECTYPE,18,TAPER
SECDATA,9,-11.4,7.99,29.6
SECDATA,10,-11.4,7.00,54.6
!~~~~~~~~~~~~~~~~~~定义19号变截面（交界墩4'-5截面）
SECTYPE,19,TAPER
SECDATA,11,-11.4,7.00,54.6
SECDATA,12,-11.4,6.27,79.6
!~~~~~~~~~~~~~~~~~~定义20号变截面（交界墩5-6截面）
SECTYPE,20,TAPER
SECDATA,12,-11.4,6.27,79.6
SECDATA,13,-11.4,5.58,106.6
!-------------------------21#~25#截面暂时不用了，因为JJD也改成四分之一建模方式，不再需要-----------------------------!
!~~~~~~~~~~~~~~~~~~定义21号变截面（交界墩1-2截面）
!SECTYPE,21,TAPER
!SECDATA,6,-11.4,-8.98,4.6
!SECDATA,7,-11.4,-8.51,16.6
!!~~~~~~~~~~~~~~~~~~定义22号变截面（交界墩2'-3截面）
!SECTYPE,22,TAPER
!SECDATA,8,-11.4,-8.51,16.6
!SECDATA,9,-11.4,-7.99,29.6
!!~~~~~~~~~~~~~~~~~~定义23号变截面（交界墩3-4截面）
!SECTYPE,23,TAPER
!SECDATA,9,-11.4,-7.99,29.6
!SECDATA,10,-11.4,-7.00,54.6
!!~~~~~~~~~~~~~~~~~~定义24号变截面（交界墩4'-5截面）
!SECTYPE,24,TAPER
!SECDATA,11,-11.4,-7.00,54.6
!SECDATA,12,-11.4,-6.27,79.6
!!~~~~~~~~~~~~~~~~~~定义25号变截面（交界墩5-6截面）
!SECTYPE,25,TAPER
!SECDATA,12,-11.4,-6.27,79.6
!SECDATA,13,-11.4,-5.58,106.6
!-------------------------END-----------------------------!


!----------------------------------------------------!
!~~~~~~~~~~~~~~~~~~定义D1~D5(1#墩到5#墩截面)截面~~~~~~~~~~~~~~~~~~~~!
!----------------------START-------------------------!
!~~~~~~~~~~~~~~~~~~定义26号截面（1#墩1-1截面）
SECTYPE,26,BEAM,MESH
SECOFFSET,USER
SECREAD,D1_1-1,SECT,,MESH
!~~~~~~~~~~~~~~~~~~定义27号截面（1#墩2-2截面）(2-2截面偏心为上部)
SECTYPE,27,BEAM,RECT
SECDATA,4,3
SECOFFSET,USER,0,1.5
!~~~~~~~~~~~~~~~~~~定义28号截面（1#墩3-3截面）
SECTYPE,28,BEAM,MESH
SECOFFSET,USER
SECREAD,D1_3-3,SECT,,MESH
!~~~~~~~~~~~~~~~~~~定义29号截面（2#墩1-1截面）
SECTYPE,29,BEAM,MESH
SECOFFSET,USER
SECREAD,D2_1-1,SECT,,MESH
!~~~~~~~~~~~~~~~~~~定义30号截面（2#墩2-2截面）(2-2截面偏心为上部)
SECTYPE,30,BEAM,RECT
SECDATA,3.5,3
SECOFFSET,USER,0,1.5
!~~~~~~~~~~~~~~~~~~定义31号截面（2#墩3-3截面）
SECTYPE,31,BEAM,MESH
SECOFFSET,USER
SECREAD,D2_3-3,SECT,,MESH
!~~~~~~~~~~~~~~~~~~定义32号截面（3#墩1-1截面）
SECTYPE,32,BEAM,MESH
SECOFFSET,USER
SECREAD,D3_1-1,SECT,,MESH
!~~~~~~~~~~~~~~~~~~定义33号截面（4#、5#墩1-1截面）(偏心为中心)
SECTYPE,33,BEAM,RECT
SECDATA,3.5,3
SECOFFSET,USER
!-------------------------END-----------------------------!


!----------------------------------------------------!
!~~~~~~~~~~~~~~~~~~定义LXL(连续梁截面)等截面~~~~~~~~~~~~~~~~~~~~!
!----------------------START-------------------------!
!~~~~~~~~~~~~~~~~~~定义34号截面（连续梁1-1截面）
SECTYPE,34,BEAM,MESH
SECOFFSET,USER
SECREAD,LXL_1-1,SECT,,MESH
!~~~~~~~~~~~~~~~~~~定义35号截面（连续梁2-2截面）
SECTYPE,35,BEAM,MESH
SECOFFSET,USER
SECREAD,LXL_2-2,SECT,,MESH
!~~~~~~~~~~~~~~~~~~定义36号截面（连续梁3-3截面）
SECTYPE,36,BEAM,MESH
SECOFFSET,USER
SECREAD,LXL_3-3,SECT,,MESH
!-------------------------END-----------------------------!



!~~~~~~~~~~~~~~~~~~定义LXL(连续梁截面)变截面~~~~~~~~~~~~~~~~~~~~!
!----------------------START-------------------------!
!---------------定义小里程侧变截面37#~40#
!~~~~~~~~~~~~~~~~~~定义37号变截面（连续梁1-2截面）
SECTYPE,37,TAPER
SECDATA,34,-67.4,0,114.1
SECDATA,35,-39.9,0,114.1!对应连续梁的2、3号点
!~~~~~~~~~~~~~~~~~~定义38号变截面（连续梁2-3截面）
SECTYPE,38,TAPER
SECDATA,35,-39.9,0,114.1
SECDATA,36,-15.15,0,114.1!对应连续梁的3、4号点
!~~~~~~~~~~~~~~~~~~定义39号变截面（连续梁3-2截面）
SECTYPE,39,TAPER
SECDATA,36,-7.65,0,114.1
SECDATA,35,17.1,0,114.1!对应连续梁的6、7号点
!~~~~~~~~~~~~~~~~~~定义40号变截面（连续梁2-1截面）
SECTYPE,40,TAPER
SECDATA,35,17.1,0,114.1
SECDATA,34,44.6,0,114.1!对应连续梁的7、8号点

!---------------定义大里程侧变截面41#~44#
!~~~~~~~~~~~~~~~~~~定义41号变截面（连续梁1-2截面）
SECTYPE,41,TAPER
SECDATA,34,398.6,0,114.1
SECDATA,35,426.1,0,114.1!对应连续梁的18、19号点
!~~~~~~~~~~~~~~~~~~定义42号变截面（连续梁2-3截面）
SECTYPE,42,TAPER
SECDATA,35,426.1,0,114.1
SECDATA,36,450.85,0,114.1!对应连续梁的19、20号点
!~~~~~~~~~~~~~~~~~~定义43号变截面（连续梁3-2截面）
SECTYPE,43,TAPER
SECDATA,36,458.35,0,114.1
SECDATA,35,483.1,0,114.1!对应连续梁的22、23号点
!~~~~~~~~~~~~~~~~~~定义44号变截面（连续梁2-1截面）
SECTYPE,44,TAPER
SECDATA,35,483.1,0,114.1
SECDATA,34,510.6,0,114.1!对应连续梁的23、24号点
!-------------------------END-----------------------------!





!~~~~~~~~~~~~~建立小里程左侧四分之一模型~~~~~~~~~~~~~~~~~~~~!
!~~~~~~~~~~~~~建立骨架1-5#和骨架6-20#~~~~~~~~~~~~~~~~~~~~!
/INPUT,GJ1-5-1,MAC
/INPUT,GJ6-20-1,MAC
!~~~~~~~~~~~~~建立合拢段骨架~~~~~~~~~~~~~~~~~~~~!
/INPUT,HLD1,MAC
!~~~~~~~~~~~~~~~~~~建立1/4外包混凝土~~~~~~~~~~~~~~~~~~~~!
/INPUT,HNTB1-5-1,MAC
/INPUT,HNTB6-7-1,MAC
/INPUT,HNTB8-20-1,MAC
/INPUT,HLDB1,MAC
!----------------------------END-----------------------------!







!~~~~~~~~~~~划分所有单元(交界墩除外)~~~~~~~~~~~~~!
!~~~~~~~~~~~划分所有线单元
LSEL,S,TYPE,,1
LMESH,ALL
!~~~~~~~~~~~划分所有面单元
ASEL,S,TYPE,,2
AMESH,ALL
!----------------------END-----------------------!





!~~~~~~~~~~~~~~~~~~建立交界墩~~~~~~~~~~~~~~~~~~~~!
!-------------------START------------------------!
/INPUT,JJD,MAC
!--------------------END-----------------------!






!~~~~~~~~~~~对称四分之一结构的所有单元(交界墩除外)形成全桥结构~~~~~~~~~~~~~!
!-------------------START------------------------!
/INPUT,SYMM,MAC
!--------------------END-----------------------!




!~~~~~~~~~~~融合所有重合节点
ALLSEL
NUMMRG,NODE
!----------------------END-----------------------!








!~~~~~~~~~~~~~--------求解模块-----------~~~~~~~~~~~~~~~~~~~~!
!-------------------------START-----------------------------!
FINI
/SOLU
ALLSEL,ALL
ACEL,,,9.8


KWPLAN,1,1002,1001,1003
CSYS,4
NSEL,S,LOC,Z,-0.01,0.01
D,ALL,ALL
CSYS,0

A=-KX(1002)+445
B=KY(1002)
C=KZ(1002)
D=KP(A,B,C)

A=-KX(1001)+445
B=KY(1001)
C=KZ(1001)
E=KP(A,B,C)

A=-KX(1003)+445
B=KY(1003)
C=KZ(1003)
F=KP(A,B,C)

KWPLAN,1,D,E,F
CSYS,4
NSEL,S,LOC,Z,-0.01,0.01
D,ALL,ALL
CSYS,0

!!!定义交界墩墩地约束!!!!
NSEL,S,LOC,X,-11.4
NSEL,R,LOC,Z,4.6
D,ALL,ALL
NSEL,S,LOC,X,11.4+445
NSEL,R,LOC,Z,4.6
D,ALL,ALL
ALLSEL,ALL


ANTYPE,0
NLGEOM,ON
AUTOTS,OFF
OUTRES,ALL,LAST
NROPT,FULL
!-------------第1荷载步
!-------------模拟钢管架立好后尚未浇筑管内混凝土状态下全桥应力
TIME,1
NSUBST,3
ESEL,S,TYPE,,2
EKILL,ALL

LSEL,NONE
*DO,I,1,20

*DO,Z,1,4
LSEL,A,,,NSHNT%Z%%I%
LSEL,A,,,NXHNT%Z%%I%
LSEL,A,,,WSHNT%Z%%I%
LSEL,A,,,WXHNT%Z%%I%
*ENDDO

*ENDDO


*DO,I,1,4
LSEL,A,,,HLDHNT%I%
*ENDDO

ESLL,S
EKILL,ALL

ALLSEL
ACEL,,,9.8
SOLVE
!-------------第2-21荷载步
!-------------模拟逐段灌注钢管内的混凝土
*DO,I,1,20
TIME,I+1
NSUBST,3
LSEL,NONE
*DO,Z,1,4
LSEL,A,,,NSHNT%Z%%I%
LSEL,A,,,NXHNT%Z%%I%
LSEL,A,,,WSHNT%Z%%I%
LSEL,A,,,WXHNT%Z%%I%
*ENDDO

ESLL,S
EALIVE,ALL

ALLSEL
ACEL,,,9.8
SOLVE
*ENDDO
!-------------第22荷载步
!-------------模拟逐段灌注合拢段钢管内的混凝土
TIME,22
NSUBST,3
LSEL,NONE
*DO,I,1,4
LSEL,A,,,HLDHNT%I%
*ENDDO

ESLL,S
EALIVE,ALL

ALLSEL
ACEL,,,9.8
SOLVE
!-------------第23-42荷载步
!-------------模拟逐段浇筑混凝土板
*DO,I,1,20
TIME,I+22
NSUBST,3
ASEL,NONE

*DO,Z,1,4
ASEL,A,,,B%Z%%I%
*ENDDO
ESLA,S
EALIVE,ALL
ALLSEL
ACEL,,,9.8
SOLVE
*ENDDO
!-------------第43荷载步
!-------------模拟浇筑合拢段板
TIME,43
NSUBST,3
ASEL,NONE
*DO,I,1,4
ASEL,A,,,HLDB%I%
*ENDDO
ESLA,S
EALIVE,ALL
ALLSEL
ACEL,,,9.8
SOLVE


FINISH
!---------------------------END-----------------------------!






!~~~~~~~~~~~~~~~~~~后处理模块~~~~~~~~~~~~~~~~~~~~!
!--------------------START-----------------------!
/POST1

!---------------------------------------!
!~~~~以下命令用于求钢管的最大应力~~~~~~~~~!
!~~~~找轴向应力+弯曲应力的最大值~~~~~~~~!
!---------------------------------------!
*ASK,HZB,Load Step Number
!HZB代表关键荷载步1(骨架合拢),22(管内混凝土灌注完毕),43(混凝土板浇筑完毕)
SET,HZB
LSEL,NONE
*DO,I,1,20
LSEL,A,,,WS1%I%
LSEL,A,,,WX1%I%
LSEL,A,,,NS1%I%
LSEL,A,,,NX1%I%
*ENDDO
ESLL

!-----钢管I端最大应力
ETABLE,SDIRI,SMISC,31
ETABLE,SBZTI,SMISC,34
ETABLE,SBZBI,SMISC,35
SADD,SIGI_1,SDIRI,SBZTI
SADD,SIGI_2,SDIRI,SBZBI
PLETAB,SIGI_1
/WAIT,3
PLETAB,SIGI_2
/WAIT,3
PRETAB,SIGI_1,SIGI_2


!!-----钢管J端最大应力（先不用）
!ETABLE,SDIRJ,SMISC,36
!ETABLE,SBZTJ,SMISC,39
!ETABLE,SBZBJ,SMISC,40
!SADD,SIGJ_1,SDIRJ,SBZTJ
!SADD,SIGJ_2,SDIRJ,SBZBJ
!PLETAB,SIGJ_1,SIGJ_2
!PRETAB,SIGJ_1,SIGJ_2
!---------------------END------------------------!



!---------------------------------------!
!~~~~以下命令用于求管内混凝土的最大应力~~~~~~~~~!
!~~~~找轴向应力+弯曲应力的最大值~~~~~~~~!
!---------------------------------------!
LSEL,NONE
*DO,I,1,20
LSEL,A,,,WSHNT1%I%
LSEL,A,,,WXHNT1%I%
LSEL,A,,,NSHNT1%I%
LSEL,A,,,NXHNT1%I%
*ENDDO
ESLL
!-----钢管内混凝土I端最大应力
ETABLE,SDIRI,SMISC,31
ETABLE,SBZTI,SMISC,34
ETABLE,SBZBI,SMISC,35
SADD,SIGI_1,SDIRI,SBZTI
SADD,SIGI_2,SDIRI,SBZBI
PLETAB,SIGI_1
/WAIT,3
PLETAB,SIGI_2
/WAIT,3
PRETAB,SIGI_1,SIGI_2


!!-----钢管内混凝土J端最大应力（先不用）
!ETABLE,SDIRJ,SMISC,36
!ETABLE,SBZTJ,SMISC,39
!ETABLE,SBZBJ,SMISC,40
!SADD,SIGJ_1,SDIRJ,SBZTJ
!SADD,SIGJ_2,SDIRJ,SBZBJ
!PLETAB,SIGJ_1,SIGJ_2
!PRETAB,SIGJ_1,SIGJ_2
!---------------------END------------------------!




!---------------------------------------!
!~~~~以下命令用于求混凝土板的最大压应力~~~~~~~~~!
!---------------------------------------!
ESEL,S,TYPE,,2
ETABLE,S3D1,NMISC,3
ETABLE,S3D2,NMISC,8
ETABLE,S3D3,NMISC,13
ETABLE,S3D4,NMISC,18
PLETAB,S3D1
/WAIT,3
PLETAB,S3D2
/WAIT,3
PLETAB,S3D3
/WAIT,3
PLETAB,S3D4
/WAIT,3
PRETAB,S3D1,S3D2,S3D3,S3D4



